Aula 12 – Procedimentos

ROTINAS (procedimentos e funções)

Rotina é um procedimento repetitivo, que você coloca dentro de um procedimento/função. Depois é só chamar o procedimento/função para executar toda a rotina.

Ele fez um "detector de pesado" e disse pra copiar.

Vou mostrar só o Procedimento:

Dentro de var, colocar 'Procedimento' nome_do_procedimento(), 'inicio' e 'FimProcedimento'.

    algoritmo
    var

    Procedimento Topo()
    inicio
        Código aqui
    FimProcedimento
    inicio

    fimalgoritmo

PASSAGEM DE PARÂMETRO

Tipo 1: POR VALOR.

    algoritmo "FazSomaProcedimento"
        var
    X, Y: Inteiro
    Procedimento Soma(A, B:Inteiro)
    inicio
        Escreval("Recebi o valor",A)
        Escreval("Recebi o valor",B)
        Escreval("A soma entre os dois é", A+B)
    FimProcedimento
    inicio
        X <- 5
        Y <- 3
        Soma(X,Y)
    fimalgoritmo

Note que o parâmetro não necessarimanete tem o mesmo nome da variável (em algumas linguagens é possível).

[Do que me recordo, ou sei até hoje, parâmetro faz parte da assinatura da função, enquanto o argumento é o valor que entra ali. Ou seja, o parâmetro é o espaço da variável, enquanto argumento é o valor colocado no parâmetro.]

ESCOPO é o local onde uma determinada variável irá funcionar.

'Global' é o escopo inteiro.
'Local' é localizado em um procedimento.

(Via de regra) Os parâmetros têm escopo local.

Tipo 2: POR REFERÊNCIA.

Isso acaba tornando o parâmetro, de certa forma, global...

É que, na verdade,agora a referência de X e Y é passada para A e B. Não somente o valor. Isso faz com que ao alterar A e B dentro do procedimento, X e Y sejam alterados fora.

-------

Exemplo de PASSAGEM DE VALOR (passagem de parâmetro por valor):

    algoritmo "SomaParametroReferencia"
    var
        X, Y: Inteiro
    Procedimento Soma(A,B:Inteiro)
    inicio
        A <- A + 1
        B <- B + 2
        Escreval("Valor de A =", A)
        Escreval("Valor de B =", B)
        Escreval("Soma A + B =", A+B)
    FimProcedimento
    inicio
        X <- 4
        Y <- 8
        Soma(X,Y)
        Escreval("Valor de X =", X)
        Escreval("valor de Y =", Y)
    fimalgoritmo

Isso faz X e Y não serem alterados.

-------

Exemplo de PASSAGEM DE REFERÊNCIA(passagem de parâmetro por referência):

    algoritmo "SomaParametroReferencia"
    var
        X, Y: Inteiro
    Procedimento Soma(var A,B:Inteiro)
    inicio
        A <- A + 1
        B <- B + 2
        Escreval("Valor de A =", A)
        Escreval("Valor de B =", B)
        Escreval("Soma A + B =", A+B)
    FimProcedimento
    inicio
        X <- 4
        Y <- 8
        Soma(X,Y)
        Escreval("Valor de X =", X)
        Escreval("valor de Y =", Y)
    fimalgoritmo

Agora A e B tem 'var' na frente. Isso faz, com que ao invés do valor, seja passada a referência, portanto X e Y serão alterados quando A e B foram alterados.

No Viasualg, na execução passo a passo (F8), enquanto o procedimento rodava X e Y ainda não estavam alterados, porém ao finalizar o procedimento, eles foram imediatamente receberam os novos valores.

-------

Exemplo de Fibonacci:

    algoritmo "Fibonacci"
    var
        C, T1,T2: Inteiro
    Procedimento ProximoFibonacci(var A,B:Inteiro)
                var C: Inteiro
    inicio
        C := A + B
        Escreva(C)
        A := B
        B := C
    FimProcedimento
    inicio
        T1 <- 0
        Escreva(T1)
        T2 <- 1
        Escreva(T2)
        Para C := 3 ate 10 faca
            ProximoFibonacci(T1,T2)
        FimPara
    fimalgoritmo


Ele usou o termo ':=' (dois pontos e igual) para dizer 'A' recebe 'B', assim:

    A := B

Com isso, 'A' COPIA/recebe O VALOR de 'B'.

Já em 'Procedimento ProximoFibonacci(var A,B:Inteiro)', A faz referência a T1, em outras palavras 'A' é um apelido de 'T1'. De maneira mais formal: “A é um parâmetro por referência que aponta para T1.”
